#!/bin/sh
":"; exec emacs --quick --script "$0" "$@" # -*-mode: lisp-interaction; lexical-binding: t-*-

;;; Commentary:
;; Write archive-contents out as JSON.

;;; Code:

(require 'url-handlers)

(setq debug-on-error t)

(defun my/write-json-for-archive (url out-file)
  "Convert archive-contents of an archive at URL into JSON.
URL is the same archive URL that would go into `package-archives'.
If OUT-FILE is nil, print to stdout instead."
  (let* (archive-content)
    (let ((buf (url-retrieve-synchronously
                (format "%s/archive-contents" url))))
      (with-temp-buffer
        ;; insert body from `buf' into the current buffer
        (url-insert buf)
        (decode-coding-region (point-min) (point-max) 'utf-8)
        (setq archive-content (cdr (read (buffer-string))))))
    (with-temp-buffer
      (cl-letf* ((print-alist-orig (symbol-function 'json--print-alist))
                 (print-array-orig (symbol-function 'json--print-array))
                 ((symbol-function 'json--print-array)
                  ;; Some cons cell somewhere is being printed as an array
                  ;; This is only really caught in the maintainer list
                  (lambda (obj)
                    (if (and (consp obj)
                             (or (stringp (car obj))
                                 (not (car obj)))
                             (stringp (cdr obj)))
                        (json--print-map (list (cons (car obj) (cdr obj))))
                      (funcall print-array-orig obj))))
                 ((symbol-function 'json--print-alist)
                  (lambda (obj)
                    ;; Override for version lists
                    (if (and (listp obj)
                             (equal 1 (length obj))
                             (ignore-errors
                               (seq-every-p #'numberp (car obj))))
                        (json--print-array (car obj))
                      (funcall print-alist-orig obj)))))
        (let ((json-encoding-pretty-print t))
          (insert (json-encode archive-content)))
        ;; START = nil: write everything; END is ignored in this case
        ;; FILENAME is OUT-FILE
        ;; Don't use append mode; VISIT is not t or nil or a string
        ;; This is the same way it's called as within `with-temp-file'.
        (if (and out-file (not (equal out-file "-")))
            (write-region nil nil out-file nil 0)
          (princ (buffer-string)))))
    (when (and out-file (not (equal out-file "-")))
      (message "Written to %s" out-file))))
(if (or (equal "-h" (car argv))
        (equal "--help" (car argv))
        (not (cadr argv))
        (not (car argv)))
    (message "Usage: <this file> <outfile> <url>
<outfile> can be \"-\", which means stdout.")
  (let ((url (cadr argv))
        (file (car argv)))
    (my/write-json-for-archive url file)))

(setq argv nil)
